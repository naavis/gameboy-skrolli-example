INCLUDE "macros.inc"

Section "Initializer",HOME[$100]
  jp main

Section "MainCode",HOME[$150]
main:
  ; Disable whole LCD
  WaitVBlankStart ; Wait for V-blank
  ld A, %00000000
  ld [$FF40], A

  ; Reset sprite color palette to default
  ld A, %11100100
  ld [$FF48], A

  ; Load 16 bytes of sprite tile data to VRAM
  ld D, 16
  ld BC, sprite_data
  ld HL, $8000
.load_loop:
  ld A, [BC]
  inc BC
  ld [HL], A
  inc HL
  dec D
  jr nz, .load_loop

  ; Clear OAM memory
  ld HL, $FE00
.clear_loop:
  ld A, 0
  ld [HL], A
  inc L
  ld A, L
  cp $A0
  jr nz, .clear_loop

  ; Add sprite data entry to OAM
  ld A, 80
  ld [$FE00], A
  ld A, 84
  ld [$FE01], A
  ld A, 0
  ld [$FE02], A
  ld [$FE03], A

  ; Enable LCD and sprites
  ld A, %10000010
  ld [$FF40], A

.loop:
  halt
  jr .loop

sprite_data:
  DW `00111100
  DW `11111111
  DW `13311331
  DW `11111111
  DW `13111131
  DW `11333311
  DW `11111111
  DW `00111100
